package org.openmicroscopy.dsl

import groovy.transform.CompileStatic
import org.gradle.api.Action
import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.Task
import org.gradle.api.plugins.ExtensionAware
import org.gradle.api.plugins.JavaPlugin
import org.gradle.api.plugins.JavaPluginConvention
import org.gradle.api.publish.maven.plugins.MavenPublishPlugin
import org.gradle.api.tasks.SourceSet
import org.openmicroscopy.dsl.extensions.DslExtension
import org.openmicroscopy.dsl.extensions.VelocityExtension
import org.openmicroscopy.dsl.tasks.GeneratorBaseTask

@CompileStatic
class DslPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {
        // Apply the base plugin
        project.plugins.apply(DslPluginBase)

        DslExtension dsl = project.extensions.getByType(DslExtension)
        VelocityExtension velocity = ((ExtensionAware) dsl).extensions.findByType(VelocityExtension)

        // Set default for velocity config
        velocity.checkEmptyObjects = false

        // Set default dir for files generated by .combined files
        dsl.outputDir = "src/generated"

        // React to java plugin
        configureForJavaPlugin(project, dsl)
    }

    static void configureForJavaPlugin(Project project, DslExtension dsl) {
        project.plugins.withType(JavaPlugin) { JavaPlugin java ->
            // .ome.xml files
            dsl.omeXmlFiles = project.files("src/main/resources/mappings")

            // Default template dir (src/
            dsl.databaseTypes = project.files("src/main/resources/properties")

            // Default template dir (src/
            dsl.templates = project.files("src/main/resources/templates")

            // Configure default outputDir
            JavaPluginConvention javaConvention =
                    project.convention.getPlugin(JavaPluginConvention)

            SourceSet main =
                    javaConvention.sourceSets.getByName(SourceSet.MAIN_SOURCE_SET_NAME)

            main.java.srcDirs "$dsl.outputDir/java"
            main.resources.srcDirs "$dsl.outputDir/resources"

            // Configure compileJava task to depend on our tasks
            project.tasks.named("compileJava").configure(new Action<Task>() {
                @Override
                void execute(Task t) {
                    t.dependsOn = project.tasks.withType(GeneratorBaseTask)
                }
            })
        }
    }

    // ToDo: fill this functionality in to handle jar naming
    static void configureForMavenPublish(Project project) {
        project.plugins.withType(MavenPublishPlugin) { MavenPublishPlugin plugin ->

        }
    }

}
